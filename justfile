install:
    @echo "ðŸš€ Creating virtual environment using uv"
    uv sync --python 3.14 --all-groups
    uv run --locked prek install

prek-refresh:
    @echo "ðŸš€ Refreshing prek hooks"
    uv run --locked prek autoupdate
    uv run --locked prek clean
    uv run --locked prek install

check:
    @echo "ðŸš€ Checking lock file consistency with 'pyproject.toml'"
    uv sync --locked --all-groups
    @echo "ðŸš€ Linting code: Running prek"
    uv run --locked prek run -a
    @echo "ðŸš€ Static type checking: Running ty"
    uv run --locked ty check
    @echo "ðŸš€ Checking for obsolete dependencies: Running deptry"
    uv run --locked deptry src

test:
    @echo "ðŸš€ Testing code: Running pytest"
    uv run --locked python -m pytest -n auto --cov-report term-missing:skip-covered --cov=src tests/ --durations=10

nox:
    @echo "ðŸš€ Launching nox sessions"
    uvx nox

build:
    @echo "ðŸš€ Removing build artifacts"
    rm -rf dist/
    @echo "ðŸš€ Building source distribution and wheel"
    uv build --no-sources
    @echo "ðŸš€ Smoke test whell"
    uv run --isolated --no-project --with dist/*.whl tests/smoke_test.py
    @echo "ðŸš€ Smoke test source distribution"
    uv run --isolated --no-project --with dist/*.tar.gz tests/smoke_test.py

publish:
    @echo "ðŸš€ Publishing package"
    uv publish

build-and-publish: build publish

docs:
    @echo "ðŸš€ Serving docs"
    uv run --locked mkdocs serve

docs-test:
    @echo "ðŸš€ Testing docs build"
    uv run --locked mkdocs build --strict

docs-deploy:
    @echo "ðŸš€ Deploying docs"
    uv run --locked mkdocs gh-deploy --force
